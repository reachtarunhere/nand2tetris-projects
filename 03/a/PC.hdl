// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // idea is to keep the undefined behaviour in line with the built in chip
    // priority: reset>load>inc
    Mux4Way16(a=incrementedOut, b=in, sel[1]=reset, sel[0]=load, out=muxOut);
    Or8Way(in[0]=reset, in[1]=load, in[2]=inc, out=loadRegister);
    Register(in=muxOut, load=loadRegister, out=out, out=incInput);
    Inc16(in=incInput, out=incrementedOut);
}
